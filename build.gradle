apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

version = "2.0.9"

repositories {
    mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '3.8'
}

test {
  scanForTestClasses = false
  include "**/JSONTests.class"
}

jar {
    manifest {
        attributes 'Implementation-Title': "Stringtree JSON reader, writer and validator", 'Implementation-Version': version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Stringtree JSON'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A lightwight, standards-compliant JSON reader, writer and validator'
        url 'http://stringtree.org/stringtree-json.html'

        scm {
          connection 'scm:git:https://github.com/efficacy/stringtree-json.git'
          developerConnection 'scm:git:git@github.com:efficacy/stringtree-json.git'
          url 'https://github.com/efficacy/stringtree-json'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'efficacy'
            name 'Frank Carver'
            email 'frank@efsol.com'
          }
        }
      }
    }
  }
}